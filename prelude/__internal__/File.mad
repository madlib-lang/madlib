#iftarget js

import type { Wish } from "Wish"
import { Wish } from "Wish"

#- import fs from "fs" -#


write :: String -> String -> Wish String String
export write = (path, content) =>
  Wish((bad, good) => #- {
    fs.writeFile(path, content, (err) => {
      if (err) {
        bad(err);
      }
      else {
        good(path)
      }
    })
  } -#)


writeBytes :: String -> ByteArray -> Wish String String
export writeBytes = (path, bytes) =>
  Wish((bad, good) => #- {
    fs.writeFile(path, bytes, (err) => {
      if (err) {
        bad(err);
      }
      else {
        good(path)
      }
    })
  } -#)


read :: String -> Wish String String
export read = (path) => Wish((bad, good) => #- {
  fs.readFile(path, "utf8", (err, d) => {
    if (err) {
      bad(err);
    }
    else {
      good(d)
    }
  })
}-#)

exists :: String -> Wish String String
export exists = (path) => Wish((bad, good) => #- {
  fs.access(path, fs.constants.F_OK, (err) => {
    if (err) {
      bad(err);
    } else {
      good(path);
    }
  });
} -#)

readDir :: String -> Wish String (List String)
export readDir = (path) => Wish((bad, good) => #- {
  fs.readdir(path, (err, files) => {
    if (err) {
      bad(err);
    } else {
      good(files);
    }
  })
} -#)

#elseif llvm
import type { Wish } from "Wish"
import { Wish } from "Wish"
import IOError from "IOError"

readFFI :: String -> (Integer -> String -> {}) -> {}
readFFI = extern "madlib__file__read"

read :: String -> Wish IOError.IOError String
export read = (path) => Wish((bad, good) =>
  readFFI(
    path,
    (libuvError, fileContent) =>
      libuvError != 0
        ? bad(IOError.fromLibuvError(libuvError))
        : good(fileContent)
  )
)

readBytesFFI :: String -> (Integer -> ByteArray -> {}) -> {}
readBytesFFI = extern "madlib__file__readBytes"

readBytes :: String -> Wish IOError.IOError ByteArray
export readBytes = (path) => Wish((bad, good) =>
  readBytesFFI(
    path,
    (libuvError, fileContent) =>
      libuvError != 0
        ? bad(IOError.fromLibuvError(libuvError))
        : good(fileContent)
  )
)


writeFFI :: String -> String -> (Integer -> {} -> {}) -> {}
writeFFI = extern "madlib__file__write"

write :: String -> String -> Wish IOError.IOError {}
export write = (path, content) => Wish((bad, good) =>
  writeFFI(
    path,
    content,
    (libuvError, result) =>
      libuvError != 0
        ? bad(IOError.fromLibuvError(libuvError))
        : good({})
  )
)


writeBytesFFI :: String -> ByteArray -> (Integer -> {} -> {}) -> {}
writeBytesFFI = extern "madlib__file__writeBytes"

writeBytes :: String -> ByteArray -> Wish IOError.IOError {}
export writeBytes = (path, content) => Wish((bad, good) =>
  writeBytesFFI(
    path,
    content,
    (libuvError, result) =>
      libuvError != 0
        ? bad(IOError.fromLibuvError(libuvError))
        : good({})
  )
)

existsFFI :: String -> (Boolean -> {}) -> {}
existsFFI = extern "madlib__file__exists"

exists :: String -> Wish {} Boolean
export exists = (path) => Wish((bad, good) =>
  existsFFI(
    path,
    good
  )
)

#endif
