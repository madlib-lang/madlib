#iftarget js

import type { Maybe } from "Maybe"

#endif

import { Just } from "Maybe"
import Wish from "Wish"
import Dictionary from "Dictionary"
import String from "String"
import List from "List"


// exit code, stdout, stderr
// export type CommandResult = CommandResult(Integer, String, String)
export alias CommandResult = {
  exitCode :: Integer,
  stdout :: String,
  stderr :: String  
}

makeCommandResult :: Integer -> String -> String -> CommandResult
makeCommandResult = (exitCode, stdout, stderr) => ({
  exitCode,
  stdout,
  stderr,
})

#iftarget js

import { Nothing } from "Maybe"

#- import { exec as execJs, spawn as spawnJs } from "child_process" -#

exec :: String -> Wish.Wish CommandResult CommandResult
export exec = (command) => Wish.Wish((bad, good) => #- {
    execJs(command, (err, stdout, stderr) => {
      if (err) {
        bad(makeCommandResult(err.code)(stdout)(stderr))
      }
      else {
        good(makeCommandResult(0)(stdout)(stderr))
      }
    })
  }
-#)

#-
const makeArgs = () => {
  let list = {}
  let start = list
  Object.keys(process.argv.slice(0)).forEach((key) => {
    list = list.n = { v: process.argv[key], n: null }
  }, {})
  return {
    n: start.n.n.n,
    v: start.n.v + " " + start.n.n.v
  }
}
-#


Argv :: List String
export Argv = #- makeArgs() -#


getEnv :: String -> Maybe String
export getEnv = (name) => (#- {
  const envVar = process.env[name];
  return envVar === undefined ? Nothing : Just(envVar)
} -#)



#-
const buildEnvImpl = (dict) => {
  let list = {}
  let start = list
  Object.keys(process.env).forEach((key) => {
    list = list.n = { v: [key, process.env[key]], n: null }
  }, {})
  return Dictionary.fromList(dict)(start.n)
}
-#

buildEnv :: Comparable a => Dictionary a String
buildEnv = extern "buildEnvImpl"

Env :: Dictionary String String
Env = buildEnv


getCurrentPath :: Wish.Wish String String
export getCurrentPath = of(#- process.cwd() -#)


getExecutablePath :: Wish.Wish String String
export getExecutablePath = of(#- process.argv[1] -#)

#elseif llvm

getArgsFFI :: {} -> List String
getArgsFFI = extern "madlib__process__internal__getArgs"


Argv :: List String
export Argv = getArgsFFI({})


getEnvFFI :: {} -> List #[String, String]
getEnvFFI = extern "madlib__process__internal__getEnv"


Env :: Dictionary String String
export Env = Dictionary.fromList(getEnvFFI({}))


execFFI :: String -> List String -> (Integer -> String -> String -> {}) -> {}
execFFI = extern "madlib__process__exec"


exec :: String -> Wish.Wish CommandResult CommandResult
export exec = (command) => Wish.Wish((bad, good) => {
  commandSplit = String.words(command)
  argv = List.drop(1, commandSplit)

  return where(List.nth(0, commandSplit)) {
    Just(cmd) =>
      execFFI(
        cmd,
        argv,
        (exitCode, stdout, stderr) =>
          exitCode != 0
            ? bad(makeCommandResult(exitCode, stdout, stderr))
            : good(makeCommandResult(exitCode, stdout, stderr))
      )
  }
})

#endif
