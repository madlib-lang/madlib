import {} from "Monad"
import {} from "Bifunctor"
import {} from "Show"

/**
 * An Either is a type which allows for modeling a disjoint union ("a" or "b", but in a safe way).
 * An Either can be a Right: a successful computation, or a Left: an unsuccessful one.
 * By capturing a value in this way, we can separate the transformation from the value contained in the Either,
 * making it easier to reason about and safer to pass around. (mnemonic: "right" == "correct")
 *
 * @since 0.0.5
 */
export type Either e a
  = Left e
  | Right a


/**
 * We can map over an Either: if it is a Right, it transforms the contained value. 
 * If it is a Left it does nothing. In this way we can freely frame the computations we want
 * to perform without being concerned as to whether the transformation was succcessful until later.
 *
 * @since 0.0.6
 * @example
 * map((x) => x + 1, Right(3))      // Right(4)
 * map((x) => x + 1, Left("error")) // Left("error")
 */
instance Functor (Either e) {
  map = (f) => where
    is Right a: Right(f(a))
    is Left  e: Left(e)
}


/**
 * By wrapping a function in `ap` we can apply it from one Either to another Either.
 * If both contained values are Rights, it returns a Right of the result. If either Either is a Left,
 * that value persists and the contained value is not transformed.
 *
 * @since 0.0.6
 * @example
 * ap(Right((x) => x + 1), Right(2))     // Right(3)
 * ap(Left("oups"), Right(2))            // Left("oups")
 * ap(Right((x) => x + 1), Left("oups")) // Left("oups")
 */
instance Applicative (Either e) {
  pure = Right
  ap = (mf, m) => where(mf)
    is Left e : Left(e)
    is Right f: map(f, m)
}


/**
 * Use `chain` when you want to flatten a potentially double-wrapped Either.
 * Instead of a Right of a Right or a Right of a Left, `chain` will flatten the contained transformation
 * and return a Right (if successful) or a Left (if unsuccessful)
 *
 * @since 0.0.6
 * @example
 * chain((x) => Right(x + 1), Right(1))   // Right(2)
 * chain((x) => Right(x + 1), Left("no")) // Left("no")
 */
instance Monad (Either e) {
  of = pure
  chain = (f) => where
    is Right a: f(a)
    is Left  e: Left(e)
}


instance Bifunctor Either {
  bimap = (leftF, rightF) => where
    is Right a: Right(rightF(a))
    is Left e : Left(leftF(e))

  mapFirst = (f, m) => mapLeft(f, m)
  mapSecond = map
}

/**
 * Use `show` to transform an Either to a string.
 *
 * @since 0.0.6
 * @example
 * show((Right(3) :: Either Number Number)) // "Right 3"
 * show((Left(3) :: Either Number Number))  // "Left 3"
 */
instance (Show e, Show a) => Show (Either e a) {
  show = where
    is Right a: "Right " ++ show(a)
    is Left  e: "Left " ++ show(e)
}

/**
 * An alias for `map`, use `mapRight` to transform the interior value of a Right.
 *
 * @since 0.0.5
 * @example
 * mapRight((x) => x + 1, Right(3)) // Right(4)
 */
mapRight :: (a -> b) -> Either e a -> Either e b
export mapRight = map


/**
 * Unlike most other Either functions, `mapLeft` is one of the few which freely transforms the
 * interior value of a Left. (It is the left-branch analogue of `mapRight`.)
 *
 * @since 0.0.5
 * @example
 * mapLeft((x) => x + 1, Left(3))  // Left(4)
 * mapLeft((x) => x + 1, Right(3)) // Right(3)
 */
mapLeft :: (e -> f) -> Either e a -> Either f a
export mapLeft = (f, m) => (where(m) {
  is Right a: Right(a)
  is Left  e: Left(f(e))
})


/**
 * Returns true if it is a Left, false otherwise.
 *
 * @since 0.0.5
 * @example
 * isLeft(Left(1))  // true
 * isLeft(Right(1)) // false
 */
isLeft :: Either e a -> Boolean
export isLeft = (either) => (where(either) {
  is Left _: true
  is _     : false
})


/**
 * Returns true if it is a Right, false otherwise.
 *
 * @since 0.0.5
 * @example
 * isRight(Left(1))  // false
 * isRight(Right(1)) // true
 */
isRight :: Either e a -> Boolean
export isRight = (either) => (where(either) {
  is Right _: true
  is _      : false
})


/**
 * Pull the value contained in the Either if it is a Right, or the given
 * fallback value if it is a Left.
 *
 * @since 0.0.5
 * @example
 * fromRight(1, Right(4)) // 4
 * fromRight(1, Left(4))  // 1
 */
fromRight :: a -> Either e a -> a
export fromRight = (a, either) => (where(either) {
  is Right x: x
  is _      : a
})
