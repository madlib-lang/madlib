#iftarget js

import type { Data } from "Data"
import type { Wish } from "Wish"
import { Wish } from "Wish"
import { BinaryData, TextData } from "Data"

#- import fs from "fs" -#


writeFile :: String -> Data -> Wish String String
export writeFile = (path, d) => where(d) {
  BinaryData(bytes) => Wish((bad, good) => #- {
      fs.writeFile(path, bytes, (err) => {
        if (err) {
          bad(err);
        }
        else {
          good(path)
        }
      })
    } -#)

  TextData(content) => Wish((bad, good) => #- {
      fs.writeFile(path, content, (err) => {
        if (err) {
          bad(err);
        }
        else {
          good(path)
        }
      })
    } -#)
}

readFile :: String -> Wish String String
export readFile = (path) => Wish((bad, good) => #- {
  fs.readFile(path, "utf8", (err, d) => {
    if (err) {
      bad(err);
    }
    else {
      good(d)
    }
  })
}-#)

exists :: String -> Wish String String
export exists = (path) => Wish((bad, good) => #- {
  fs.access(path, fs.constants.F_OK, (err) => {
    if (err) {
      bad(err);
    } else {
      good(path);
    }
  });
} -#)

readDir :: String -> Wish String (List String)
export readDir = (path) => Wish((bad, good) => #- {
  fs.readdir(path, (err, files) => {
    if (err) {
      bad(err);
    } else {
      good(files);
    }
  })
} -#)

#elseif llvm
import type { Wish } from "Wish"
import { Wish } from "Wish"
import IOError from "IOError"

readFile_ :: String -> (Integer -> String -> ()) -> ()
readFile_ = extern "readFile"

readFile :: String -> Wish IOError.IOError String
export readFile = (path) => Wish((bad, good) =>
  readFile_(
    path,
    (libuvError, fileContent) =>
      libuvError != 0
        ? bad(IOError.fromLibuvError(libuvError))
        : good(fileContent)
  )
)

readBinaryFile_ :: String -> (Integer -> Array Byte -> ()) -> ()
readBinaryFile_ = extern "readBinaryFile"

readBinaryFile :: String -> Wish IOError.IOError (Array Byte)
export readBinaryFile = (path) => Wish((bad, good) =>
  readBinaryFile_(
    path,
    (libuvError, fileContent) =>
      libuvError != 0
        ? bad(IOError.fromLibuvError(libuvError))
        : good(fileContent)
  )
)


writeFile_ :: String -> String -> (Integer -> () -> ()) -> ()
writeFile_ = extern "writeFile"

writeFile :: String -> String -> Wish IOError.IOError ()
export writeFile = (path, content) => Wish((bad, good) =>
  writeFile_(
    path,
    content,
    (libuvError, result) =>
      libuvError != 0
        ? bad(IOError.fromLibuvError(libuvError))
        : good(())
  )
)


writeBinaryFile_ :: String -> Array Byte -> (Integer -> () -> ()) -> ()
writeBinaryFile_ = extern "writeBinaryFile"

writeBinaryFile :: String -> Array Byte -> Wish IOError.IOError ()
export writeBinaryFile = (path, content) => Wish((bad, good) =>
  writeBinaryFile_(
    path,
    content,
    (libuvError, result) =>
      libuvError != 0
        ? bad(IOError.fromLibuvError(libuvError))
        : good(())
  )
)

exists_ :: String -> (Boolean -> ()) -> ()
exists_ = extern "fileExists"

exists :: String -> Wish () Boolean
export exists = (path) => Wish((bad, good) =>
  exists_(
    path,
    good
  )
)

#endif
