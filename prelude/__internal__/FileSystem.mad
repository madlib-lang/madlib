import W from "Wish"
import B from "Binary"

export type Data
  = TextData String
  | BinaryData B.ByteArray


#- import fs from "fs" -#


writeFile :: String -> Data -> W.Wish e String
export writeFile = (path, d) => where(d) {
  is BinaryData (B.ByteArray arr): W.Wish((bad, good) => #- {
      fs.writeFile(path, arr, (err) => {
        if (err) {
          bad(err);
        }
        else {
          good(path)
        }
      })
    } -#)
  is TextData content: W.Wish((bad, good) => #- {
      fs.writeFile(path, content, (err) => {
        if (err) {
          bad(err);
        }
        else {
          good(path)
        }
      })
    }
  -#)
}

readFile :: String -> W.Wish e String
export readFile = (path) => W.Wish((bad, good) => #- {
  fs.readFile(path, "utf8", (err, d) => {
    if (err) {
      bad(err);
    }
    else {
      good(d)
    }
  })
}-#)

exists :: String -> W.Wish e String
export exists = (path) => W.Wish((bad, good) => #- {
  fs.access(path, fs.constants.F_OK, (err) => {
    if (err) {
      bad(err);
    } else {
      good(path);
    }
  });
} -#)
