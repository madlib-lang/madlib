#iftarget js

import type { Data } from "Data"
import type { Wish } from "Wish"
import { Wish } from "Wish"
import { BinaryData, TextData } from "Data"

#- import fs from "fs" -#


writeFile :: String -> Data -> Wish String String
export writeFile = (path, d) => where(d) {
  BinaryData(bytes) => Wish((bad, good) => #- {
      fs.writeFile(path, bytes, (err) => {
        if (err) {
          bad(err);
        }
        else {
          good(path)
        }
      })
    } -#)

  TextData(content) => Wish((bad, good) => #- {
      fs.writeFile(path, content, (err) => {
        if (err) {
          bad(err);
        }
        else {
          good(path)
        }
      })
    } -#)
}

readFile :: String -> Wish String String
export readFile = (path) => Wish((bad, good) => #- {
  fs.readFile(path, "utf8", (err, d) => {
    if (err) {
      bad(err);
    }
    else {
      good(d)
    }
  })
}-#)

exists :: String -> Wish String String
export exists = (path) => Wish((bad, good) => #- {
  fs.access(path, fs.constants.F_OK, (err) => {
    if (err) {
      bad(err);
    } else {
      good(path);
    }
  });
} -#)

readDir :: String -> Wish String (List String)
export readDir = (path) => Wish((bad, good) => #- {
  fs.readdir(path, (err, files) => {
    if (err) {
      bad(err);
    } else {
      good(files);
    }
  })
} -#)

#elseif llvm
import type { Wish } from "Wish"
import { Wish } from "Wish"
import IOError from "IOError"

readFileFFI :: String -> (Integer -> String -> ()) -> ()
readFileFFI = extern "madlib__filesystem__readFile"

readFile :: String -> Wish IOError.IOError String
export readFile = (path) => Wish((bad, good) =>
  readFileFFI(
    path,
    (libuvError, fileContent) =>
      libuvError != 0
        ? bad(IOError.fromLibuvError(libuvError))
        : good(fileContent)
  )
)

readBinaryFileFFI :: String -> (Integer -> Array Byte -> ()) -> ()
readBinaryFileFFI = extern "madlib__filesystem__readBinaryFile"

readBinaryFile :: String -> Wish IOError.IOError (Array Byte)
export readBinaryFile = (path) => Wish((bad, good) =>
  readBinaryFileFFI(
    path,
    (libuvError, fileContent) =>
      libuvError != 0
        ? bad(IOError.fromLibuvError(libuvError))
        : good(fileContent)
  )
)


writeFileFFI :: String -> String -> (Integer -> () -> ()) -> ()
writeFileFFI = extern "madlib__filesystem__writeFile"

writeFile :: String -> String -> Wish IOError.IOError ()
export writeFile = (path, content) => Wish((bad, good) =>
  writeFileFFI(
    path,
    content,
    (libuvError, result) =>
      libuvError != 0
        ? bad(IOError.fromLibuvError(libuvError))
        : good(())
  )
)


writeBinaryFileFFI :: String -> Array Byte -> (Integer -> () -> ()) -> ()
writeBinaryFileFFI = extern "madlib__filesystem__writeBinaryFile"

writeBinaryFile :: String -> Array Byte -> Wish IOError.IOError ()
export writeBinaryFile = (path, content) => Wish((bad, good) =>
  writeBinaryFileFFI(
    path,
    content,
    (libuvError, result) =>
      libuvError != 0
        ? bad(IOError.fromLibuvError(libuvError))
        : good(())
  )
)

existsFFI :: String -> (Boolean -> ()) -> ()
existsFFI = extern "madlib__filesystem__fileExists"

exists :: String -> Wish () Boolean
export exists = (path) => Wish((bad, good) =>
  existsFFI(
    path,
    good
  )
)

#endif
