/**
 * Sum two numbers
 *
 * @since 0.0.5
 */
add :: Number a => a -> a -> a
export add = (a, b) => (a + b)

/**
 * Calculate the difference between two numbers
 *
 * @since 0.0.5
 */
substract :: Number a => a -> a -> a
export substract = (a, b) => (a - b)

/**
 * Return the product of two numbers
 *
 * @since 0.0.5
 */
multiply :: Number a => a -> a -> a
export multiply = (a, b) => (a * b)

/**
 * Calculate how many times a number can be contained in another number
 *
 * @since 0.0.5
 */
divide :: Float -> Float -> Float
export divide = (a, b) => (a / b)


/**
 * Return the euclidean division of one number by another
 *
 * @since 0.0.5
 */
mod :: Integer -> Integer -> Integer
export mod = (a, b) => (a % b)


#iftarget js

/**
 * Find the number which, when multiplied by itself equals the given number. 
 *
 * @since 0.0.5
 */
sqrt :: Float -> Float
export sqrt = (n) => #- { return Math.sqrt(n) } -#

round :: Float -> Float
export round = (n) => #- { return Math.round(n) } -#

ceil :: Float -> Float
export ceil = (n) => #- { return Math.ceil(n) } -#

floor :: Float -> Float
export floor = (n) => #- { return Math.floor(n) } -#

pow :: Float -> Float -> Float
export pow = (a, b) => #- { return Math.pow(a, b) } -#

random :: a -> Float
export random = #- { return Math.random } -#

#elseif llvm

sqrt :: Float -> Float
export sqrt = extern "madlib__math__sqrt"

round :: Float -> Float
export round = extern "madlib__math__round"

ceil :: Float -> Float
export ceil = extern "madlib__math__ceil"

floor :: Float -> Float
export floor = extern "madlib__math__floor"

pow :: Float -> Float -> Float
export pow = extern "madlib__math__pow"

random :: a -> Float
export random = extern "madlib__math__random"

#endif


/**
 * Find the result of a number times itself
 *
 * @since 0.0.5
 */
square :: Number a => a -> a
export square = (n) => n * n


/**
 * Returns the smallest number from the two given
 * @since 0.12.0
 */
min :: Number a => a -> a -> a
export min = (a, b) =>
  a > b
    ? b
    : a


/**
 * Returns the biggest number from the two given
 * @since 0.12.0
 */
max :: Number a => a -> a -> a
export max = (a, b) =>
  a > b
    ? a
    : b
