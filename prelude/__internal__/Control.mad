import type { Maybe } from "Maybe"
import { Just, Nothing } from "Maybe"


/**
 * Executes the given function until it returns false.
 *
 * @since 0.13.0
 * @example
 * x = 0
 * while(() => {
 *   x = x + 1
 *   return x != 5
 * })
 * // x == 5
 */
while :: ({} -> Boolean) -> {}
export while = (f) =>
  if (f()) {
    while(f)
  }


/**
 * Given a start value, a predicate function and a transformation function,
 * it applies the transformation function to the value until the predicate
 * function returns false.
 *
 * @since 0.13.0
 * @example
 * loop(1, (x) => x < 10, (x) => x * 2) // 16
 */
loop :: a -> (a -> Boolean) -> (a -> a) -> a
export loop = (start, pred, evaluate) =>
  pred(start)
    ? loop(evaluate(start), pred, evaluate)
    : start


/**
 * A function to perform loops. Might be useful as an alternative to recursion
 * when the stack size might be a concern.
 * It takes an initial value, a transformation function that must return a Maybe.
 * It returns the value contained in the Just in the last iteration before a
 * Nothing was returned.
 *
 * @since 0.7.0
 * @example
 * maybeLoop(
 *   1,
 *   (x) => x < 10
 *     ? Just(x * 2)
 *     : Nothing
 * )
 * // 16
 */
maybeLoop :: a -> (a -> Maybe a) -> a
export maybeLoop = (start, evaluate) => where(evaluate(start)) {
  Just(x) =>
    maybeLoop(x, evaluate)

  Nothing =>
    start
}
