import { assertEquals, test } from "Test"
import {
  empty,
  fromList,
  get,
  insert,
  keys,
  len,
  mapWithKey,
  merge,
  values
} from "./Dictionary"
import { Just, Nothing } from "Maybe"
import IO from "IO"

test("fromList", (_) =>
  assertEquals(
    fromList([#[1, 2], #[2, 2]]),
    pipe(
      insert(1, 2),
      insert(2, 2)
    )(empty)
  )
)

test("fromList - should dedupe keys", (_) =>
  assertEquals(
    fromList([#[1, 2], #[2, 2], #[2, 3]]),
    {{ 1: 2, 2: 3 }}
  )
)

test("insert - no existing", (_) =>
  assertEquals(insert(1, 2, empty), fromList([#[1, 2]]))
)

test("insert - update", (_) =>
  assertEquals(insert(1, 7, {{ 1: 2 }}), fromList([#[1, 7]]))
)

test("get - Just", (_) =>
  assertEquals(get("john", fromList([#["john", { age: 32 }]])), Just({ age: 32 }))
)

test("get - Nothing", (_) =>
  assertEquals(get("john", fromList([#["paul", { age: 32 }]])), Nothing)
)

test("keys", (_) =>
  assertEquals(keys({{ true: "Go for it", false: "NOGO" }}), [false, true])
)

test("values", (_) =>
  assertEquals(values({{ true: "Go for it", false: "NOGO" }}), ["NOGO", "Go for it"])
)

test("len", (_) =>
  assertEquals(len(fromList([#["Sidney", 3], #["New York", 18], #["London", 2]])), 3)
)

test("map", (_) =>
  assertEquals(
    map((x) => x / 2, fromList([#["Sidney", 3], #["New York", 18], #["London", 2]])),
    fromList([#["Sidney", 1.5], #["New York", 9], #["London", 1]])
  )
)

test("mapWithKey", (_) =>
  assertEquals(
    mapWithKey(
      (city, x) => city == "Sidney" ? 9 : x / 2,
      fromList([#["Sidney", 3], #["New York", 18], #["London", 2]])
    ),
    fromList([#["Sidney", 9], #["New York", 9], #["London", 1]])
  )
)

test("merge", (_) => {
  dictLeft = fromList([#["Sidney", 3], #["New York", 18], #["London", 2]])
  dictRight = fromList([#["Paris", 13], #["Berlin", 5], #["London", 17]])

  expected = fromList([
    #["Sidney", 3],
    #["New York", 18],
    #["London", 17],
    #["Paris", 13],
    #["Berlin", 5]
  ])

  return assertEquals(merge(dictLeft, dictRight), expected)
})
