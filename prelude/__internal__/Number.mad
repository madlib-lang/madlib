import type { Maybe } from "Maybe"
import { EQUAL, LESS, MORE } from "Compare"
import {} from "Scan"
import {} from "Show"


instance Comparable Integer {
  compare = (a, b) => a > b ? MORE : a == b ? EQUAL : LESS
}

instance Comparable Byte {
  compare = (a, b) => a > b ? MORE : a == b ? EQUAL : LESS
}

instance Comparable Float {
  compare = (a, b) => a > b ? MORE : a == b ? EQUAL : LESS
}


instance Show Integer {
  show = inspect
}

instance Show Byte {
  show = inspect
}

instance Show Float {
  show = inspect
}


#iftarget js
import { Just, Nothing } from "Maybe"

scanInteger :: Number a => String -> Maybe a
scanInteger = (str) => (#- {
  const n = parseInt(str)
  return isNaN(n) ? Nothing : Just(n)
} -#)



scanFloat :: Number a => String -> Maybe a
scanFloat = (str) => (#- {
  const n = parseFloat(str)
  return isNaN(n) ? Nothing : Just(n)
} -#)

#elseif llvm

scanInteger :: String -> Maybe Integer
scanInteger = extern "madlib__number__scanInteger"

scanFloat :: String -> Maybe Float
scanFloat = extern "madlib__number__scanFloat"

scanByte :: String -> Maybe Byte
scanByte = extern "madlib__number__scanByte"

#endif

instance Scan Integer {
  scan = scanInteger
}

instance Scan Float {
  scan = scanFloat
}


// conversions
#iftarget js

integerToFloat :: Integer -> Float
export integerToFloat = (a) => #- a -#

integerToByte :: Integer -> Byte
export integerToByte = (a) => #- Uint8Array.from([a])[0] -#

byteToFloat :: Byte -> Float
export byteToFloat = (a) => #- a -#

byteToInteger :: Byte -> Integer
export byteToInteger = (a) => #- a -#

floatToInteger :: Float -> Integer
export floatToInteger = #- Math.trunc -#

floatToByte :: Float -> Byte
export floatToByte = (a) => #- Uint8Array.from([Math.trunc(a)])[0] -#

#elseif llvm

integerToFloat :: Integer -> Float
export integerToFloat = extern "madlib__number__intToFloat"

integerToByte :: Integer -> Byte
export integerToByte = extern "madlib__number__intToByte"

byteToFloat :: Byte -> Float
export byteToFloat = extern "madlib__number__byteToFloat"

byteToInteger :: Byte -> Integer
export byteToInteger = extern "madlib__number__byteToInt"

floatToInteger :: Float -> Integer
export floatToInteger = extern "madlib__number__floatToInt"

floatToByte :: Float -> Byte
export floatToByte = extern "madlib__number__floatToByte"

#endif

#iftarget js

// TODO: implement for llvm backend
/**
 * Formats a a number given as the first parameter to a string containing the
 * amount of numbers after the . given by the second parameter.
 * @since 0.4.0
 * @example
 * formatDecimal(3.14159, 2) // "3.14"
 */
formatDecimal :: Number a => a -> Float -> String
export formatDecimal = (a, x) => #-{
  const n = x.toFixed(a)
  return isNaN(n) ? "0" : n
}-#

// TODO: Move to List and write with recursion. Add llvm version.
range :: Integer -> Integer -> List Integer
export range = (a, b) => #-{
  const out = []
  let x = a
  while (x < b) {
    out.push(x)
    x += 1
  }
  return out
}-#
#endif
