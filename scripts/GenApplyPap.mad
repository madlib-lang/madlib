import type { Maybe } from "Maybe"

import IO from "IO"
import List from "List"
import String from "String"
import Process from "Process"
import { Just, Nothing } from "Maybe"
import Date from "Date"
import { fst, snd } from "Tuple"


dateComment :: String
dateComment = pipe(
  Date.toISOString,
  (dateString) => #[String.slice(0, 10, dateString), String.slice(11, 19, dateString)],
  (timeInfo) => `// generated automatically on the ${fst(timeInfo)} at ${snd(timeInfo)} UTC`
)(Date.now())


CPP_FILE_HEADER =
`${dateComment}
#include <gc.h>
#include "apply-pap.hpp"
#include <cstdarg>
#include <iostream>


// Partial application

#ifdef __cplusplus
extern "C" {
#endif
`

CPP_FILE_FOOTER =
`#ifdef __cplusplus
}
#endif
`

FUNCTION_START =
`void *__applyPAP__(void *pap, int32_t argc, ...) {
  va_list argv;
  va_start(argv, argc);

  PAP_t *unwrappedPAP = (PAP_t *)pap;
  int32_t ENV_SIZE = unwrappedPAP->arity - unwrappedPAP->missingArgCount;
  int32_t ARITY = unwrappedPAP->arity;
`

FUNCTION_END =
`  printf("__applyPAP__ case not handled, argc: %d, ENV_SIZE: %d, ARITY: %d\\n", argc, ENV_SIZE, ARITY);

  return NULL;
}

`

KNOWN_CALL_START =
`
  if (argc >= unwrappedPAP->missingArgCount) {
    void *result = (void *)NULL;
    void **env = (void **)unwrappedPAP->env;
`

KNOWN_CALL_END =
`    va_end(argv);
    return result;
  } `

OVERLOAD_START =
`    if (argc > unwrappedPAP->missingArgCount) {
      int argsLeft = argc - unwrappedPAP->missingArgCount;
`

OVERLOAD_END = "    }\n"

UNDERLOAD =
`else {
    // We push the args to a newly allocated PAP
    int NEXT_ENV_SIZE = argc + ENV_SIZE;
    PAP_t *newPAP = (PAP_t *)GC_MALLOC(sizeof(PAP_t));
    newPAP->fn = unwrappedPAP->fn;
    newPAP->arity = unwrappedPAP->arity;
    newPAP->missingArgCount = unwrappedPAP->missingArgCount - argc;

    void **env = (void **)unwrappedPAP->env;
    void **newEnv = (void**) GC_MALLOC(sizeof(void*) * NEXT_ENV_SIZE);
    int i = 0;
    for (i = 0; i<ENV_SIZE; i++) {
      newEnv[i] = env[i];
    }
    for (; i<NEXT_ENV_SIZE; i++) {
      void *arg = va_arg(argv, void*);
      newEnv[i] = arg;
    }

    va_end(argv);
    newPAP->env = newEnv;
    return newPAP;
  }
`


HPP_FILE_TOP =
`${dateComment}
#ifndef APPLY_PAP_H
#define APPLY_PAP_H

#include <math.h>
#include <stdint.h>

`

HPP_FILE_BOTTOM =
`
typedef struct PAP {
  void *fn;
  int32_t arity;
  int32_t missingArgCount;
  void *env;
} PAP_t;


#ifdef __cplusplus
extern "C" {
#endif

void *__applyPAP__(void *pap, int32_t argc, ...);

#ifdef __cplusplus
}
#endif

#endif // APPLY_PAP_H
`


generateFnAssignment :: Integer -> String
generateFnAssignment = (arity) => {
  params = pipe(
    List.repeat("void *"),
    String.join(", ")
  )(arity)

  return `void *(*fn)(${params}) = (void*(*)(${params}))unwrappedPAP->fn;`
}


makeIndentation :: Integer -> String
makeIndentation = (indent) => String.repeat(' ', indent)


makeArgNamesWithStart :: Integer -> Integer -> List String
makeArgNamesWithStart = (start, count) => pipe(
  List.range(start),
  map((argIndex) => `arg${inspect(argIndex)}`)
)(count)


makeArgNames :: Integer -> List String
makeArgNames = makeArgNamesWithStart(1)


makeArgAssignments :: Integer -> List String -> String
makeArgAssignments = (indent, argNames) => pipe(
  map((n) => `${makeIndentation(indent)}void *${n} = va_arg(argv, void*);\n`),
  List.reduce(mconcat, "")
)(argNames)


makeFunctionCall :: String -> List String -> String
makeFunctionCall = (functionName, argNames) => pipe(
  String.join(", "),
  (args) => `${functionName}(${args})`
)(argNames)


generateKnownCallCase :: Integer -> String
generateKnownCallCase = (arity) => {
  caseStart = `      case ${inspect(arity)}: {\n`
  fnAssignment = `        ${generateFnAssignment(arity)}\n`
  allArgs = makeIndexedArgNames(arity)
  case = `        void **args = (void**) GC_MALLOC(sizeof(void*) * (${inspect(arity)}));
        int i = 0;
        for (; i < ENV_SIZE; i++) { args[i] = env[i]; }
        for (; i < ARITY; i++) { args[i] = va_arg(argv, void*); }
        result = ${makeFunctionCall("fn", allArgs)};
        break;\n`
  caseEnd = `        break;\n      }\n`

  return caseStart ++ fnAssignment ++ case ++ caseEnd
}

generateKnownCallCases :: Integer -> String
generateKnownCallCases = (maxArity) => {
  cases = pipe(
    List.range(1),
    map(generateKnownCallCase),
    List.reduce(mconcat, "")
  )(maxArity + 1)

  return `    switch (ARITY) {\n${cases}    }\n`
}


makeIndexedArgNames :: Integer -> List String
makeIndexedArgNames = (count) => pipe(
  List.range(0),
  map((index) => `args[${inspect(index)}]`)
)(count)


generateOverloadCase :: Integer -> String
generateOverloadCase = (argsLeft) => {
  argNames = makeIndexedArgNames(argsLeft)
  allArgs = ["result", "argsLeft", ...argNames]
  argsRead = `          void **args = (void**) GC_MALLOC(sizeof(void*) * ${inspect(argsLeft)});
          for (int i = 0; i < ${inspect(argsLeft)}; i++) { args[i] = va_arg(argv, void*); }\n`
  fnCall = makeFunctionCall("__applyPAP__", allArgs)


  return `        case ${inspect(argsLeft)}: {\n${argsRead}          result = ${fnCall};\n          break;\n        }\n`
}


generateOverloadCases :: Integer -> String
generateOverloadCases = (maxArity) => {
  cases = pipe(
    List.range(1),
    map(generateOverloadCase),
    List.reduce(mconcat, "")
  )(maxArity + 1)

  return `      switch (argsLeft) {\n${cases}      }\n`
}


generateNextEnvCase :: Integer -> Integer -> String
generateNextEnvCase = (envSize, nextEnvSize) => {
  papType = `PAPEnv_${inspect(nextEnvSize)}_t`
  newEnv = `${makeIndentation(12)}${papType} *newEnv = (${papType} *)GC_MALLOC(sizeof(${papType}));\n`
  returnNewPAP = `${makeIndentation(12)}va_end(argv);\n${makeIndentation(12)}newPAP->env = newEnv;\n${makeIndentation(12)}return newPAP;\n`

  newArgNames = makeArgNamesWithStart(envSize, nextEnvSize)
  newArgAssignments = makeArgAssignments(12, newArgNames)
  oldArgNames = pipe(
    List.range(0),
    map((n) => `env->arg${inspect(n)}`)
  )(envSize)

  newEnvAssignments = pipe(
    List.mapWithIndex((n, i) => `${makeIndentation(12)}newEnv->arg${inspect(i)} = ${n};\n`),
    List.reduce(mconcat, "")
  )(mconcat(oldArgNames, newArgNames))

  return `          case ${inspect(nextEnvSize)}: {\n${newEnv}${newArgAssignments}${newEnvAssignments}${returnNewPAP}          }\n`
}


generateCppFile :: Integer -> {}
generateCppFile = (maxArity) => {
  IO.put(CPP_FILE_HEADER)
  IO.put(FUNCTION_START)
  IO.put(KNOWN_CALL_START)
  IO.put(generateKnownCallCases(maxArity))
  IO.put(OVERLOAD_START)
  IO.put(generateOverloadCases(maxArity))
  IO.put(OVERLOAD_END)
  IO.put(KNOWN_CALL_END)
  IO.put(UNDERLOAD)
  IO.put(FUNCTION_END)
  IO.put(CPP_FILE_FOOTER)

  return {}
}


generatePAPTypeDefinition :: Integer -> String
generatePAPTypeDefinition = (arity) => pipe(
  makeArgNamesWithStart(0),
  map((argName) => `  void *${argName};\n`),
  List.reduce(mconcat, ""),
  (fields) => `typedef struct PAPEnv_${inspect(arity)} {\n${fields}} PAPEnv_${inspect(arity)}_t;\n\n`
)(arity)


generatePAPTypeDefinitions :: Integer -> String
generatePAPTypeDefinitions = (maxArity) => pipe(
  List.range(1),
  map(generatePAPTypeDefinition),
  List.reduce(mconcat, "")
)(maxArity + 1)


generateHppFile :: Integer -> {}
generateHppFile = (maxArity) => {
  IO.put(HPP_FILE_TOP)
  IO.put(generatePAPTypeDefinitions(maxArity + 1))
  IO.put(HPP_FILE_BOTTOM)

  return {}
}


where (Process.Argv) {
  [_, arityStr, "CPP"] =>
    where (scan(arityStr)) {
      Just(arity) =>
        generateCppFile(arity)

      Nothing =>
        IO.putLine("ARITY needs to be an integer")
    }

  [_, arityStr, "HPP"] =>
    where ((scan(arityStr) :: Maybe Integer)) {
      Just(arity) =>
        generateHppFile(arity)

      Nothing =>
        IO.putLine("ARITY needs to be an integer")
    }

  _ => do {
    IO.putLine("You must pass two args, the kind of file and the max arity!")
    IO.putLine("Usage:")
    IO.putLine("GenApplyPap ARITY FILE_TYPE")
    IO.putLine("ARITY - must be an integer")
    IO.putLine("FILE_TYPE - must be 'CPP' or 'HPP'")
    return {}
  }
}
